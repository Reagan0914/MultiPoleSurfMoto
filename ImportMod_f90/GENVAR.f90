!****************************************************************************
!
!  SUBROUTINE GENVAR: 
!
!  INPUT: 
!
!  OUTPUT: 
!
!****************************************************************************
	
	SUBROUTINE GENVAR()

	USE GLOBAL

	USE IMSL

	IMPLICIT NONE

	LOGICAL FLAG

	INTEGER :: I,J,K

	INTEGER :: TMP_DATA(3*N_F,2),TMP_IDX(3*N_F/2,2), ENUM(2), LOCT, ID, TMP2

	DOUBLE PRECISION :: LAM
	
	DOUBLE PRECISION :: TMP(3),RA(3),RB(3),RC(3),RP(3),AP(3)

	SRULER=(0.75D0*T0/PI)**(1.0D0/3.0D0)
	V_DATA=V_DATA/SRULER
	
	N_E=N_F*3/2

	ALLOCATE(E_DATA(N_E,4))

!GENERATE EDGE DATA E_DATA, FORMAT: VERTEX 1, VERTEX 2, FACE 1, FACE 2

	DO I=1,N_F

	  TMP_DATA(3*I-2,:)=F_DATA(I,1:2)
	  TMP_DATA(3*I-1,:)=F_DATA(I,2:3)
	  TMP_DATA(3*I,:)=F_DATA(I,3:1:-2)

	END DO

	K=0

	DO I=1,N_F*3-1

	  DO J=I+1,N_F*3

	    FLAG=((TMP_DATA(J,1)==TMP_DATA(I,1)).AND.(TMP_DATA(J,2)==TMP_DATA(I,2))).OR.&
		     ((TMP_DATA(J,1)==TMP_DATA(I,2)).AND.(TMP_DATA(J,2)==TMP_DATA(I,1)))
		  
		IF (FLAG) THEN

		  K=K+1
	      E_DATA(K,1:2)=TMP_DATA(I,:)
		  E_DATA(K,3)=(I-1)/3+1
		  E_DATA(K,4)=(J-1)/3+1

		END IF

	  END DO
	   
	END DO

!GENERATE EDGE LENGTH DATA LE_DATA

	ALLOCATE(LE_DATA(N_E))

	DO I=1,N_E
	
	  TMP=V_DATA(E_DATA(I,1),:)-V_DATA(E_DATA(I,2),:)
	  LE_DATA(I)=NORM(TMP)

	END DO
	
!GENERATE EDGE NORMAL VECTOR DATA NE_DATA

	ALLOCATE(NE_DATA(N_E,6))

	DO I=1,N_E

	  RB=V_DATA(E_DATA(I,1),:)
	  RC=V_DATA(E_DATA(I,2),:)
	  
	  DO J=1,2

		K=SUM(F_DATA(E_DATA(I,J+2),:))-SUM(E_DATA(I,1:2))
		RA=V_DATA(K,:)
		LAM=DOT_PRODUCT(RA-RC,RB-RC)/DOT_PRODUCT(RB-RC,RB-RC)
		RP=LAM*RB+(1-LAM)*RC
		AP=RP-RA
		NE_DATA(I,3*J-2:3*J)=AP/NORM(AP)

	  END DO

	END DO

!GENERATE FACE NORMAL VECTOR DATA NF_DATA

	ALLOCATE(NF_DATA(N_F,3))

	DO I=1,N_F

	  RA=V_DATA(F_DATA(I,1),:)
	  RB=V_DATA(F_DATA(I,2),:)
	  RC=V_DATA(F_DATA(I,3),:)
	  CALL CROSS(RB-RA,RC-RB,TMP)
	  NF_DATA(I,:)=TMP/SQRT(DOT_PRODUCT(TMP,TMP))
	  
	END DO

!GENERATE THE RELATION VARIANTS BETWEEN SECTIONS: 
!SEC_DATA(I,J,K): I FACE, J EDGE ORDER OF I, K=1: NEIGHBOR FACE NUMBER, K=2: EDGE ORDER IN THE NEIGHBOR FACE 

	ALLOCATE(SEC_DATA(N_F,3,2))

	DO I=1,N_F

	  DO J=1,3
		
		ENUM(1)=F_DATA(I,MOD(-1+J,3)+1)
		ENUM(2)=F_DATA(I,MOD(J,3)+1)
		
		DO K=1,N_E
		  
		  FLAG=((ENUM(1)==E_DATA(K,1)).AND.(ENUM(2)==E_DATA(K,2))).OR.&
		       ((ENUM(1)==E_DATA(K,2)).AND.(ENUM(2)==E_DATA(K,1)))
		  
		  IF (FLAG) THEN
			
			ID=SUM(E_DATA(K,3:4))-I
			TMP2=SUM(ENUM)
			IF (TMP2==F_DATA(ID,1)+F_DATA(ID,2)) THEN
			  LOCT=1
			ELSE IF (TMP2==F_DATA(ID,2)+F_DATA(ID,3)) THEN
			  LOCT=2
			ELSE IF (TMP2==F_DATA(ID,3)+F_DATA(ID,1)) THEN
			  LOCT=3
			END IF

			EXIT

		  END IF

		END DO

		SEC_DATA(I,J,1)=ID
		SEC_DATA(I,J,2)=LOCT

	  END DO

	END DO
	
	RETURN

	END SUBROUTINE GENVAR
